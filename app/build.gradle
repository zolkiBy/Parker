plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.zolki.parker"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "0.0.1" + "." + versionCode

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        File localPropsFile = rootProject.file("local.properties")
        if (localPropsFile.exists()) {
            Properties props = new Properties()
            props.load(localPropsFile.newDataInputStream())

            def mapsKey = props.getProperty("maps.key", "")
            if (mapsKey.isEmpty()) {
                println("WARNING: maps key is empty")
            } else {
                println("Google map key: $mapsKey")
            }

            //noinspection SpellCheckingInspection
            def golemioKey = props.getProperty("golemio.key", "")
            if (golemioKey.isEmpty()) {
                println("WARNING: golemio key is empty")
            } else {
                println("Golemio key: $golemioKey")
            }

            manifestPlaceholders = [mapsKey: mapsKey]

            buildConfigField "String", "GOLEMIO_KEY", "\"${golemioKey}\""
        } else {
            println("WARNING: local.properties file not exist")
        }
    }

    buildFeatures {
        viewBinding true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation "androidx.constraintlayout:constraintlayout:$constraint_layout"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines"

    // Networking
    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit"
    implementation "com.google.code.gson:gson:$gson"

    // Koin
    implementation "org.koin:koin-android:$koin"
    implementation "org.koin:koin-android-scope:$koin"
    implementation "org.koin:koin-android-viewmodel:$koin"

    // View Binding
    implementation "com.kirich1409.viewbindingpropertydelegate:vbpd-noreflection:$view_binding"

    // Navigation Component
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_component"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_component"

    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle"

    // Google maps
    implementation "com.google.android.gms:play-services-maps:$google_maps"
    implementation "com.google.maps.android:android-maps-utils:$google_maps_utils"

    // Testing
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}